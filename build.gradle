plugins {
    id "java-library"
    id "base.base-conventions"
    id "base.example-sourceset"
    id "base.fill-build-constants"
    id "thingl.publishing-conventions"
    id "net.raphimc.class-token-replacer" version "1.1.7"
    id "xyz.wagyourtail.jvmdowngrader" version "1.3.6"
}

repositories {
    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/snapshots"

        content {
            includeGroupByRegex "net\\.lenni0451(\\..+)?"
        }
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroupByRegex "com\\.github\\..+"
        }
    }
}

dependencies {
    compileOnly "org.jetbrains:annotations:26.0.2-1"
    api("org.joml:joml:1.10.8") {
        exclude group: "org.jetbrains.kotlin" // https://github.com/JOML-CI/JOML/issues/352
    }
    api("org.joml:joml-primitives:1.10.0") {
        exclude group: "org.joml", module: "joml"
    }
    api "it.unimi.dsi:fastutil:8.5.18"
    api "net.lenni0451:glsl-processor:0.3.0-20260104.200704-5"
    api "net.lenni0451.commons:core:1.9.2"
    api "net.lenni0451.commons:logging:1.9.2"

    // LWJGL needs to be provided by the application using ThinGL
    compileOnly "org.lwjgl:lwjgl:3.4.0"
    compileOnly "org.lwjgl:lwjgl-opengl:3.4.0"

    // Optional dependencies
    compileOnly "org.lwjgl:lwjgl-glfw:3.4.0"
    compileOnly "org.lwjgl:lwjgl-sdl:3.4.0"
    compileOnly("org.lwjglx:lwjgl3-awt:0.2.3") {
        exclude group: "org.lwjgl", module: "lwjgl"
        exclude group: "org.lwjgl", module: "lwjgl-opengl"
        exclude group: "org.lwjgl", module: "lwjgl-vulkan"
    }
    compileOnly "org.lwjgl:lwjgl-stb:3.4.0"
    compileOnly "org.lwjgl:lwjgl-freetype:3.4.0"
    compileOnly "org.lwjgl:lwjgl-harfbuzz:3.4.0"
    compileOnly "org.lwjgl:lwjgl-meshoptimizer:3.4.0"
    compileOnly "io.github.earcut4j:earcut4j:2.2.2"
    compileOnly "com.github.RaphiMC:gif-reader:0bdb781a96"
    compileOnly "com.twelvemonkeys.imageio:imageio-webp:3.13.0"
    compileOnly "com.github.weisj:jsvg:2.0.0"

    // Example dependencies
    ["natives-windows", "natives-windows-arm64", "natives-linux", "natives-linux-arm64", "natives-macos-arm64"].each {
        exampleImplementation "org.lwjgl:lwjgl:3.4.0:$it"
        exampleImplementation "org.lwjgl:lwjgl-opengl:3.4.0:$it"
        exampleImplementation "org.lwjgl:lwjgl-glfw:3.4.0:$it"
        exampleImplementation "org.lwjgl:lwjgl-sdl:3.4.0:$it"
        exampleImplementation "org.lwjgl:lwjgl-stb:3.4.0:$it"
        exampleImplementation "org.lwjgl:lwjgl-freetype:3.4.0:$it"
        // exampleImplementation "org.lwjgl:lwjgl-harfbuzz:3.4.0:$it" // Already included in FreeType
        exampleImplementation "org.lwjgl:lwjgl-meshoptimizer:3.4.0:$it"
    }
}

downgradeJar {
    downgradeTo = JavaVersion.VERSION_17
    archiveVersion = project.version + "+java17"
}
shadeDowngradedApi {
    downgradeTo = JavaVersion.VERSION_17
    archiveVersion = project.version + "+java17"
}
build.finalizedBy("shadeDowngradedApi")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadeDowngradedApi) {
                classifier = "java17"
            }
        }
    }
}

// Disable module metadata generation because it causes issues with the downgraded jar
tasks.withType(GenerateModuleMetadata).configureEach {
    it.enabled = false
}
