plugins {
    id "java-library"
    id "base.base-conventions"
    id "base.example-sourceset"
    id "base.fill-build-constants"
    id "thingl.publishing-conventions"
    id "net.raphimc.class-token-replacer" version "1.1.7"
}

repositories {
    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/snapshots"
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroupByRegex "com\\.github\\..+"
        }
    }
    maven {
        name = "Sonatype Snapshots"
        url = "https://central.sonatype.com/repository/maven-snapshots"

        content {
            includeModule "org.lwjgl", "lwjgl-sdl"
        }
    }
}

dependencies {
    compileOnly "org.jetbrains:annotations:26.0.2-1"
    api("org.joml:joml:1.10.8") {
        exclude group: "org.jetbrains.kotlin" // https://github.com/JOML-CI/JOML/issues/352
    }
    api("org.joml:joml-primitives:1.10.0") {
        exclude group: "org.joml", module: "joml"
    }
    api "it.unimi.dsi:fastutil:8.5.18"
    api "net.lenni0451.commons:core:1.9.0"
    api "net.lenni0451.commons:logging:1.9.0"

    // LWJGL needs to be provided by the application using ThinGL
    compileOnly "org.lwjgl:lwjgl:3.3.6"
    compileOnly "org.lwjgl:lwjgl-opengl:3.3.6"
    compileOnly "org.lwjgl:lwjgl-stb:3.3.6"

    // Optional dependencies
    compileOnly "org.lwjgl:lwjgl-glfw:3.3.6"
    compileOnly("org.lwjgl:lwjgl-sdl:3.4.0-SNAPSHOT") {
        transitive = false
    }
    compileOnly "org.lwjgl:lwjgl-freetype:3.3.6"
    compileOnly "org.lwjgl:lwjgl-harfbuzz:3.3.6"
    compileOnly "org.lwjgl:lwjgl-meshoptimizer:3.3.6"
    compileOnly "io.github.earcut4j:earcut4j:2.2.2"
    compileOnly "com.github.RaphiMC:gif-reader:0bdb781a96"
    compileOnly "com.twelvemonkeys.imageio:imageio-webp:3.12.0"
    compileOnly "com.github.weisj:jsvg:2.0.0"

    // Example dependencies
    ["natives-windows", "natives-windows-arm64", "natives-linux", "natives-linux-arm64"].each {
        exampleImplementation "org.lwjgl:lwjgl:3.3.6:$it"
        exampleImplementation "org.lwjgl:lwjgl-glfw:3.3.6:$it"
        exampleImplementation("org.lwjgl:lwjgl-sdl:3.4.0-SNAPSHOT:$it") {
            transitive = false
        }
        exampleImplementation "org.lwjgl:lwjgl-opengl:3.3.6:$it"
        exampleImplementation "org.lwjgl:lwjgl-stb:3.3.6:$it"
        exampleImplementation "org.lwjgl:lwjgl-freetype:3.3.6:$it"
        // exampleImplementation "org.lwjgl:lwjgl-harfbuzz:3.3.6:$it" // Already included in FreeType
        exampleImplementation "org.lwjgl:lwjgl-meshoptimizer:3.3.6:$it"
    }
}
